// @music atcommand
// changes the music for all players on the map
//
// group lv: 2
// group char lv: 99
// log: True
//
// usage:
//    @music <short name>
//
// example:
//    @music fun

-	script	@music	32767,{
    end;

OnCall:
    if (!is_evtc())
    {
        end;
    }

    // TODO: tmw-like argv splitter
    .@map$ = getmap();

    .@key$ = strtolower(.@atcmd_parameters$[0]);
    .@m$ = htget(.hash, .@key$, "Not found");

    if (.@m$ == "Not found")
    {
        .@m$ = implode(.@atcmd_parameters$[0], " ");
        message(getcharid(CHAR_ID_ACCOUNT), sprintf("Can't broadcast: %s", .@m$));
    } else {
        message(getcharid(CHAR_ID_ACCOUNT), sprintf("Now broadcasting: %s", .@m$));
        changemusic .@map$, .@m$ + ".ogg";
    }
    end;

OnMyself:
    .@key$ = strtolower(.@atcmd_parameters$[0]);
    .@m$ = htget(.hash, .@key$, "Not found");

    if (.@m$ == "Not found")
    {
        .@m$ = implode(.@atcmd_parameters$[0], " ");
        message(getcharid(CHAR_ID_ACCOUNT), sprintf("Can't play: %s", .@m$));
    } else {
        message(getcharid(CHAR_ID_ACCOUNT), sprintf("Now playing: %s", .@m$));
        changeplayermusic .@m$ + ".ogg";
    }
    end;

OnInit:
    bindatcmd "music", "@music::OnCall", 0, 99, 1;
    bindatcmd "jukebox", "@music::OnMyself", 0, 50, 0;

    .hash = htnew; // create hashtable
    htput(.hash, "forest",     "bartk - in the forest of the birches");
    htput(.hash, "adventure",  "bartk - the adventure begins");
    htput(.hash, "fun",        "eric matyas - ghoulish fun");
    htput(.hash, "surreal",    "eric matyas - surreal place");
    htput(.hash, "ocean",      "ezili - ocean sounds");
}
