// Evol scripts.
// Authors:
//    Reid
//    Travolta
// Description:
//    Sailor of Nard's La Johanne ship.
// ArtisQuests_QOnan -- quest variable:
//    0  - not started
//    1  - Q'Onan asked to find Chest
//    2  - Chest found, heading back to Q'Onan
//    3  - Quest completed

001-1,186,107,0	script	Q'Onan#001-1	NPC_QONAN,{

    .@q = getq(ArtisQuests_QOnan);
    if (.@q == 1) goto L_QuestGiven;
    if (.@q == 2) goto L_ItemFound;
    if (.@q == 3) goto L_QuestDone;

    speech S_LAST_BLANK_LINE,
        l("We haven't met, right?"),
        l("My name is Q'Onan, I'm a member of Nard's crew."),
        l("I have a lot of work here, so I don't even have time to go to the town."),
        l("Could you do a small favor for me?");

    switch (select(l("Go on."), l("I don't have the time sorry.")))
    {
        case 2:
        speech S_FIRST_BLANK_LINE | S_LAST_NEXT,
            l("No problem, see you next time.");
        closeclientdialog;
        close;
    }

    speech S_FIRST_BLANK_LINE | S_LAST_BLANK_LINE,
        l("Before I joined Nard, I used to be a thief."),
        l("I'm not proud of it, but I had to steal money from others to keep on living."),
        l("Anyway, the last time I stole something I was almost caught."),
        l("I robbed a nobleman, took a chest full of coins."),
        l("But my luck left me, because as soon as I left his house, the guards started chasing me."),
        l("It was at night, so I could barely escape. I ran to Artis hill."),
        l("I was afraid to get busted, so I decided to get rid of the evidence."),
        l("I buried the chest somewhere on that hill. It was dark, so I don't remember where to dig exactly."),
        l("I'm afraid to go there myself, but if you take the risk, you can have half of the loot."),
        l("So, what do you say?");

    select
        l("I like money! Consider it done."),
        l("Too risky, I might end up in jail. Do it yourself.");

    switch(@menu)
    {
        case 1:
            setq ArtisQuests_QOnan, 1;
            getrandompoint(178,26,182,29);
            npcdebug "Chest buriad at (" + .move__rand_x + "," + .move__rand_y + ")";
            shovel_addquest("001-1", .move__rand_x, .move__rand_y, "QOnanFoundItem");
            speech S_FIRST_BLANK_LINE | S_LAST_BLANK_LINE,
                l("Alright, you will need this shovel."),
                l("You should check on the highest part of the cliff, I was hiding there."),
                l("I hope to see you soon.");
            getitem IronShovel, 1;
            // For questlog
            setq2 ArtisQuests_QOnan, .move__rand_x+any(-1,1);
            setq3 ArtisQuests_QOnan, .move__rand_y+any(-1,1);
            close;
        case 2:
            speech S_FIRST_BLANK_LINE | S_LAST_BLANK_LINE,
                l("Come on, don't be a coward!"),
                l("Anyway, let me know if you change your mind.");
            close;
    }

L_QuestGiven:
    speech S_LAST_NEXT, lg("I asked you to do me a favor, did you forget?");
    select l("What should I do, again?");
    speech S_FIRST_BLANK_LINE,
        l("Please find the small chest, buried somewhere on Artis hill."),
        l("You should check on the highest part of the cliff, I was hiding there."),
        l("Bring it to me, and you will get your reward.");
    close;

L_ItemFound:
    speech
        l("After all this time, it was still there!"),
        lg("We are very lucky, my friend."),
        l("Let me open it with my key."),
        l("Like I promised, here is your share."),
        l("I can finally pay off my debts.");

    Zeny += 1800;
    setq ArtisQuests_QOnan, 3;
    close;

L_QuestDone:
    speech l("Howdy, partner in crime?");
    close;

OnInit:
    .sex = GENDER_HIDDEN;
    .distance = 2;
    .debug = 1;
    end;
}

function	script	QOnanFoundItem	{
    setq ArtisQuests_QOnan, 2, 0, 0;
    // getitem SmallChest, 1;
    narrator S_FIRST_BLANK_LINE,
       l("You found a small chest, surprisingly heavy for it's size."),
       l("It's probably full of coins, but no matter how hard you try to open it, you can't."),
       l("Better take it back to Q'Onan.");
    return;
}
