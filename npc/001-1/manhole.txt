// Evol scripts.
// Author:
//    Jesusalva
//    Reid
// Description:
//    A manhole near Mona's house.
// Variable:
//    ArtisQuests_MonaDad
// Quest states:
//    0  - Quest not started
//    1  - Mona explained that her dad was missing

001-1,152,52,0	script	#manhole1-001-1	NPC_NO_SPRITE,{

    .@i=manhole_interact("001-3-0");
    closeclientdialog();
    if (.@i == -1) {
        slide_or_warp("001-3-0", 152, 56);
    } else if (.@i == TrainingArrow || .@i == WoodenBow) {
        npctalk3 l("(You hear a faint sound in distance, but can't say what sound it was.)");
    }
    close;

OnInit:
    .distance = 1;
    end;
}

001-1,196,35,0	script	#manhole2-001-1	NPC_NO_SPRITE,{

    .@i=manhole_interact("001-3-0");
    closeclientdialog();
    if (.@i == -1) {
        slide_or_warp("001-3-0", 196, 36);
    } else if (.@i == TrainingArrow || .@i == WoodenBow) {
        npctalk3 l("???: Hey! Is someone there? Please, help, it's so dark down here!");
    }
    close;

OnInit:
    .distance = 1;
    end;
}

001-1,146,123,0	script	#manhole3-001-1	NPC_NO_SPRITE,{

    .@i=manhole_interact("001-3-0");
    closeclientdialog();
    if (.@i == -1) {
        // Not a fan of calling strong players as weak, so take STR in account
        // If you have enough STR, you get an Easter Egg about the engine :)
        if (readparam(bStr) < 50) {
            mesc l("The lid is sealed shut, and you are too weak to open it.");
        } else {
            mesc l("Even with your Hercules strength, this lid is hopelessly struck.");
        }
    } else if (.@i == TrainingArrow || .@i == WoodenBow) {
        // This is the wrong lid for Mundane Quest
        npctalk3 l("...");
    }
    close;

OnInit:
    .distance = 1;
    end;
}

// This lid can only be used to leave
001-1,86,131,0	script	#manhole4-001-1	NPC_NO_SPRITE,{

    .@i=manhole_interact("001-3-0");
    closeclientdialog();
    if (.@i == -1) {
        mesc l("Strangely enough, this lid can only be open from inside.");
    } else if (.@i == TrainingArrow || .@i == WoodenBow) {
        // This is the wrong lid for Mundane Quest
        npctalk3 l("...");
    }
    close;

OnInit:
    .distance = 1;
    end;
}

// To quickly create more sewer mouths we can use:
//001-1,86,131,0	duplicate(#manhole3-001-1)	#manhole4-001-1	NPC_NO_SPRITE

