// Evol scripts.
// Authors:
//    Reid
// Description:
//    Dye seller, she sells and explain how works dye cards.
// Version:
//    2016.019 "Under Construction".
// Variables:
//    ArtisQuests_Fexil
// Quest states:
//    0  -- not started
//    1  -- Lloyd warned about the quest
//    2  -- Fexil explain what he needs
//    3  -- Fexil buy every fur that the pc bring to him

001-1,47,126,0	script	Calypsan#001-1	NPC_CALYPSAN,{

    function explain_dyes {
        speech 5,
            l("Look at your equipment, can you guess what material it is made from?"),
            l("Seek a colorant for that material:"),
            l("Cotton, cashmere, leather, ..."),
            l("Zinc-Titanium Oxide, Phthalocyanides, Cadmiums, Lazulite..."),
            l("Colorant is not the yeying color. Some wear out from light, others look great but inflict itai-itai, and others become spotty or wash-away."),
            l("What yoiis should know:"),
            l("1: The type of item yoiis want to dye."),
            l("2: What material it is made from."),
            l("3: Which colorants can dye it."),
            l("4: How many cards your item can contain."),
            l("Exemple for yoiis:"),
            l("Take an @@ and a @@, yeye obtains a @@.", getitemlink(ArtisTankTop), getitemlink(RedCottonDye), getitemlink(ArtisTankTop, RedCottonDye)),
            l("If yoiis wants to see a different use for the cards he can yeye for Resa at the light armor shop for a description of the styling cards.");
    }

    function sell_dye {
        speech 5, l("For what kind of tissue?");
        .@tissue_type = select (l("Cotton"), l("Cashmere"));

        closedialog;
        switch (.@tissue_type)
        {
            case 1:
                shop "Cotton#Dye001-1";
                break;
            case 2:
                shop "Cashmere#Dye001-1";
                break;
            default:
                debugmes "Calypsan script error, tissue_type is incorrect";
                break;
        }
        close;
    }

    speech 4,
        l("Yoiis envies a rainbow in the sky?"),
        l("It is possible to yeye your cloth with my beautiful dyes."),
        l("What does yoiis want today?");

    .@fexil = getq(ArtisQuests_Fexil);

    do
    {
        menuint
            rif(.@fexil == 1, l("Have you seen Fexil?")), 0,
            menuaction(l("Trade")), 1,
            l("How do these dyes work?"), 2,
            l("I want to make my own dyes."), 3,
            menuaction(l("Quit")), 4;

        switch (@menuret)
        {
            case 0:
                setcamnpc "Fexil#001-1";
                speech 5,
                    l("Oh yeye did, his store is just beside me!"),
                    l("Yoiis should look there.");
                restorecam;

                break;
            case 1:
                sell_dye;
                close;

            case 2:
                explain_dyes;
                break;
            case 3:
                speech 5,
                    l("Yoiis is not yet ready to make his own dyes."),
                    l("But in the future I might be able to help you create some of your own.");
                break;
            case 4:
                break;
        }
    } while (@menuret != 4);

    closedialog;
    goodbye;
    close;

OnInit:
    .sex = G_FEMALE;
    .distance = 3;
    end;
}

