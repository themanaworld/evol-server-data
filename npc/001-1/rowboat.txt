// Evol scripts.
// Author:
//    Micksha, Toams
// Description:
//    The rowboat, picking up the player on small beach in order to bring it to Koga.

001-1,192,25,0	script	#Rowboat	NPC_ROWBOAT_STAND_WE,{
    if (.caller != getcharid(3)) {
        mesn;
        mesq l("Oh, hello. I am just observing the whales.");
        next;
        mesn;
        mesq l("Nobody told me anything about you, unfortunately.");
        close;
    }

    speech S_LAST_BLANK_LINE | S_LAST_NEXT,
        l("Oh, hi."),
        l("In case you don't have a commendatory letter from you-know-whom, I am just sitting here and observe the whales."),
        l("Perhaps, and only perhaps, if you bring me the latter, I can bring you to a ship that will perhaps, and only perhaps, be able to bring you to Woodland."),
        l("Do you have this commendatory letter?");
        closeclientdialog();
    if (askyesno() == ASK_YES) {
        if (getq(General_Brotherhood) >= 2)
        {
            //slide 227, 62;
            //slide 204, 62;
            PC_DEST$="Argaes";
            @timer_navio_running = 1;
            addtimer 210000, "#MarineShip::OnEvent"; // This route is 30s slower
            warp "marine-2", 40, 32;
            dispbottom l("You're smuggled aboard the Legion expedition. ACT NATURALLY!");
            .caller=0;
            .clocks=gettimetick(2);
            movenpc("#Rowboat", 192, 25);
            //setmount 1;
        }
        else
        {
            npctalk3 l("You're lying, aren't you? I heard nothing from you-know-whom!");
        }
    close;
    }

OnMoveToBeach:
    startnpctimer;
    initialmove;
end;

OnTimer1000:
    getmapxy(.@map$, .@cx, .@cy, 1);
    if (.@cx == 192 && .@cy == 25) stopnpctimer;
    else domovestep;

OnInit:
    .bodytype = BODYTYPE_3;
    .distance = 3;
    .speed = 175;
    .caller = 0;
    .clocks = 0;

    initpath "move", 202, 27,
             "move", 213, 58,
             "move", 203, 64,
             "wait", 15, 0,
             "move", 213, 58,
             "move", 202, 27,
             "move", 192, 25;
    initnpctimer;
}
