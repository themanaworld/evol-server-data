// Evol scripts.
// Authors:
//    Reid
//    Travolta
// Description:
//    Little girl playing around the hill on top of the port.
//    She wants you to find her brother.
// Variables:
//    AtrilQuests_LazyBrother = 19  -- quest var
//    LazyBrother_TreesLeft = 15    -- how many trees left to search
//    LazyBrother_TreeSearched[15]  -- whether given tree was searched
//    LazyBrother_TreeWithBrother   -- the number of tree where he's hiding
// Quest states:
//    0  - quest not started
//    1  - Katja asked help, searching the trees
//    2  - Bobo is found, "bad" ending
//    3  - player decided to tell Katja where her brother is
//    4  - "good" ending, player helped Katja

001-1,164,44,0	script	Katja#001-1	NPC_RAIJIN_GIRL,{

    function QuestReminder {
        setcam 5920, 960;
        mesq l("Remember, you have to find my brother on the hill east of here.");
        next;
        mesq l("He is probably hiding up one of the trees. Go close to each tree and look up, otherwise you won't notice him.");
        next;
        restorecam;
    }

    .@q = getq(ArtisQuests_LazyBrother);
    mesn;
    mesq l("Hey");
    next;
    switch(.@q)
    {
        case 0:
            mesq l("You look nice, do you want to help me?");
            break;
        case 1:
            mesq l("Hi, how is it going?");
            next;
            select l("Remind me, what should I do?");
            mes "";
            mesn;
            QuestReminder;
            close;
        case 2:
            mesq l("I still didn't find my brother...");
            close;
        case 3:
            mesq l("Did you find my brother?");
            next;
            select(l("Yes, and he promised to be home soon."));
            mes "";
            mesn;
            mesq l("Oh thank you so much!");
            next;
            mesq l("Please take this @@ for your help.", getitemlink(Aquada)); // Need a different reward.
            setq ArtisQuests_LazyBrother, 4;
            getitem Aquada, 1;
            getexp 500, 0;
            close;
        case 4:
            mesq l("Thank you again for your help!");
            close;
    }
    next;

    switch (select(l("Of course!"),
                   l("What kind of help do you need?"),
                   l("I don't have much time right now.")))
    {
        case 1:
            mes "";
            mesn;
            mesq l("Great!");
            emotion E_JOY;
            next;
            break;
        case 2:
            mes "";
            mesn;
            break;
        case 3:
            mes "";
            mesn;
            mesq l("Okay, I'll be here, if you change your mind.");
            emotion E_SAD;
            close;
    }

L_Story:
    mesq l("Mommy sent me to find my brother Bobo.");
    next;
    mesq l("He said he is going to climb the highest hill in Artis, which is not far from here.");
    next;
    mesq l("Our mommy doesn't allow us to go there, because it's dangerous. But he doesn't listen!");
    next;
    mesq l("Can you please go there and find him? He is probably hiding in one of those trees, like always.");
    next;
    mesq l("Because of the dense foliage, you need to go up close to the trees so you don't miss him.");
    next;

    switch (select(l("I will find him, don't worry!"),
                   l("Highest hill in Artis you say? Sounds dangerous, I might fall off.")))
    {
        case 1:
            mes "";
            mesn;
            mesq l("Thank you!");
            mes "";
            QuestReminder;
            setq ArtisQuests_LazyBrother, 1;
            LazyBrother_TreesLeft = 15;
            close;
        case 2:
            mes "";
            mesn;
            mesq l("Aw, what will I do now?");
            close;
    }

OnInit:
    .sex = G_FEMALE;
    .distance = 4;
    end;
}

