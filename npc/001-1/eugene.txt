// Evol scripts.
// Authors:
//    Reid
//    Travolta
// Description:
//    Fishman NPC
// Quest variable:
//    ArtisQuests_Fishman
// Quest stages:
//    0 - not started
//    1 - Eugene asked for items
//    2 - completed

001-1,113,120,0	script	Eugene	NPC_EUGENE,{

    .BaitID = SmallTentacles;
    .BaitCount = 10;

    narrator 4,
        l("You see a raijin boy, sitting on the edge of the dock."),
        l("He's holding a fishing rod, while gazing out at the sea.");

    .@q = getq(ArtisQuests_Fishman);
    if (.@q == 1) goto L_CheckItems;
    if (.@q == 2) goto L_QuestDone;

    speech 2,
        l("Ahoi."),
        l("Hey, check out my brand new fishing rod. I bought it just today."),
        l("I was so excited, I wanted to try it as soon as possible."),
        l("So in a hurry, I forgot to take enough bait for fishing."),
        lg("Be a friend and bring me @@ @@.", .BaitCount, getitemlink(.BaitID));

    switch (select(l("I'll be back in no time."),
                   l("Sorry, doing other things at the moment.")))
    {
        case 1:
            setq ArtisQuests_Fishman, 1;
            speech 1,
                l("Thank you. I'll wait here.");
            close;
        case 2:
            speech 1,
                l("But I'm almost out of @@...", getitemlink(.BaitID));
            close;
    }

L_CheckItems:
    if (countitem(.BaitID) < .BaitCount)
    {
        speech
            l("Sorry, but you don't have what I need for bait."),
            l("I need @@ @@.", .BaitCount, getitemlink(.BaitID));
        close;
    }

    speech
        l("That's exactly what I needed."),
        l("As a thank you, accept my old fishing rod."),
        l("It's not as good as my new one, but still useful."),
        l("Just look out at that water! There's plenty more fish there."),
        l("With my old fishing rod you can catch something every day."),
        l("You might even get lucky, and get a @@.", getitemlink(GrassCarp)),
        l("Have a good time fishing!");

    delitem .BaitID, .BaitCount;
    getitem FishingRod, 1;
    setq ArtisQuests_Fishman, 2;
    close;

L_QuestDone:
    // Idea for future: Eugene telling fishman jokes.
    speech
        l("Ahoy, @@!", strcharinfo(0)),
        l("Are the fish biting today?");
    close;

OnInit:
    .sex = G_MALE;
    .distance = 2;
    end;
}
