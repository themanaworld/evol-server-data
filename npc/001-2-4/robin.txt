// Evol scripts.
// Author:
//    Reid
// Description:
//    Librarian

001-2-4,27,33,0	script	Robin	NPC_ROBIN,{

    function need_help
    {
        speech S_LAST_NEXT,
            l("Do you need help with something?");

        switch (select(l("What kinds of books are there here?"),
                       l("Nothing.")))
        {
            case 1:
                closeclientdialog;
                npctalk3 l("Mostly manuals and tutorials but you won't find out until you open one!");
                break;
            case 2:
                closeclientdialog;
                npctalk3 l("Good day to you!");
        }
        return;
    }


    switch (rand(6))
    {
        case 0:
            npctalkonce l("No matter what people tell you, words and ideas can change the world.");
            break;
        case 1:
            npctalkonce l("In Artis they really do mythologize people when they die.");
            break;
        case 2:
            npctalkonce l("What counts in a book is like so many things, not what is on the outside, but what is on the inside.");
            break;
        case 3:
            npctalkonce l("An explorer left behind some incomplete manuscripts about the people and dangers in the land of Kaizei.");
            break;
        case 4:
            speech S_LAST_NEXT,
                l("Ow-oh!"),
                l("Introduce yourself instead of poking me around. I almost fell from the lader, savage...");

            setcamnpc "#001-2-4-Book1";

            speech S_NO_NPC_NAME,
                l("Go have a look at that shelf to my right, would you?"),
                l("You clearly need to be more public-spirited.");
            break;
        default:
            need_help;
            break;
    }

    close;

OnInit:
    .distance = 2;
    end;
}
