// TMW scripts.
// Author:
//    Micksha
// Description:
//    Crane is Snarfles' apprentice, who secretly eats mouboo steak and enjoys.

008-1,97,113,0	script	Crane	NPC_SNARFLES,{
    function foodQuest;

    mesn;
    mesq l("Heya. I am Crane, Snarfle's apprentice.");
    if (getq(General_SmearedHands) <= 3 && getq2(General_SmearedHands) != 1)
        foodQuest();
    close;

function foodQuest {
    .@q=getq(General_SmearedHands);
    next;
    mesn;
    mesq l("Don't tell him, but, I secretly love %s!", getitemlink(MoubooSteak));
    switch (.@q) {
    case 1:
        next;
        mesn strcharinfo(0);
        select
            l("Do you know a recipe for %s?", getitemname(BarbecuePlate)),
            l("Okay, I won't.");
        mes "";
        if (@menu == 2)
            break;
        mesn;
        mesq l("Ah sure, it is a simple recipe, I could even make one for you and give you the recipe, but...");
        next;
        mesn;
        mesq l("I always get smeared hands eating it and cannot play cards anymore with Snarfles.");
        next;
        mesn;
        mesq l("Well, if you find me a solution for that problem, I can give you the recipe. What do you say?");
        next;
        mesc l("WARNING: If you accept this quest, you'll be struck at the %s route!", b(l("Carnivour"))), 1;
        mesc l("This will also make %s's Quest unavailable.", b("Snarfles")), 1;
        mesc l("This decision cannot be changed later."), 1;
        next;
        mesc l("Accept this request?"), 1;
        if (askyesno() == ASK_YES) {
            setq General_SmearedHands, 2, CARNIVOROUS; // 2 = Carnivorous
            mesn;
            mesq l("Great! I'll be eager for your return!");
        }
        break;
    case 2:
        mesn;
        mesq l("Have you found a solution for my smeared hands problem?");
        next;
        select
            l("No, not yet.");
        mes "";
        mesn;
        mesq l("I've heard they're related to the earl of sandwich, so maybe a sandwich maker could help... *sigh* I want to play cards...");
        break;
    case 3:
        mesn strcharinfo(0);
        select
            l("Have you tried putting it in a bread already?"),
            l("I'll keep looking for solutions for your problem.");
        mes "";
        if (@menu == 2)
            return;
        mesn;
        mesq l("Actually... No. Have been too afraid of it tasting foul.");
        next;
        mesn strcharinfo(0);
        mesq l("Really? Tiki, Candor's chef, liked it very much.");
        next;
        mesn;
        mesq l("Wha - Really?! Tiki said it tasted good??! I'm saved! You're truly my savior!!");
        next;
        mesn;
        mesq l("I'll put the %s recipe on your %s. Thanks, you saved my day!", getitemlink(BarbecuePlate), getitemlink(RecipeBook));
        RECIPES[CraftBarbecuePlate]=true;
        getitembound BarbecuePlate, 1, IBT_ACCOUNT;
        setq1 General_SmearedHands, 4; // Finished
    default:
        return;
    }
    return;
}

OnInit:
    .distance = 4;
    end;
}
