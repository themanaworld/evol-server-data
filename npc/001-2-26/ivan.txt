// Evol scripts.
// Author:
//    Reid
// Description:
//    Alchemist NPC

001-2-26,28,30,0	script	Ivan	NPC_IVAN,{

    function ivan_description {
        speech S_FIRST_BLANK_LINE | S_LAST_NEXT,
            l("I am an alchemist, I bear the name of Ivan."),
            l("I work under the protection and the supervision of the Legion of Aemil."),
            l("Alchemical studies are prohibited in every city of the Archipelagos, do not dare to practice it in a public zone!");

        return;
    }

    function enora_quest {
        speech S_FIRST_BLANK_LINE | S_LAST_NEXT, l("How many? What kind? I can produce two dozen boxes per day.");

        select (l("5 Piberries Infusions"));

        speech S_FIRST_BLANK_LINE | S_LAST_NEXT,
            l("5 boxes... Alright, @@ are my speciality!", getitemlink (PiberriesInfusion)),
            l("I can prepare them in an hour or so!");

        select (l("You missunderstood, it's 5 potions."));

        speech S_FIRST_BLANK_LINE | S_LAST_NEXT,
            l("Why would the Legion request my help for only 5 potions?"),
            l("Is this some kind of joke?!");

        switch (select (l("Enora needs them."),
                        l("Excuse me that was a mistake.")))
        {
            case 1:
                break;
            case 2:
                narrator S_FIRST_BLANK_LINE, l("Ivan grumbles and resumes his work.");
                emotion E_UPSET;
                close;
        }

        speech S_FIRST_BLANK_LINE | S_LAST_NEXT,
            l("Oh... You should have started with this!"),
            l("I have some spare one, take them, and tell Enora that any fees are paid!");

        setq ArtisQuests_Enora, 9;
        emotion E_HAPPY;
        return;
    }

    .@enora = getq (ArtisQuests_Enora);

    if (.@enora < 8)
    {
        npctalk3 l("I'm busy, leave me alone.");
    }

    speech S_LAST_NEXT, l("Yes what do you need?");

    select
        rif(.@enora == 8, l("The Legion needs some potions.")),
        rif(.@enora >= 8, l("Who are you?")),
        menuaction(l("Quit"));

    switch (@menu)
    {
        case 1:
            enora_quest;
            break;
        case 2:
            ivan_description;
            break;
    }

    closedialog;
    goodbye;
    close;

OnInit:
    .sex = G_MALE;
    .distance = 2;
    end;
}
