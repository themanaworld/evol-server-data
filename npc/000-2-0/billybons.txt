// Evol scripts.
// Author:
//    Reid
// Description:
//    Drunk easteregg telling about the player's past.

000-2-0,20,23,0	script	Billy Bons	NPC_BILLY_BONS,{
    if (rand(5) == 2) goto L_Hic;

    mesn;
    mesq l("You? Here?");
    mesq l("How is *hic* it possible?");
    next;

    mesn "Narrator";
    mesc(l("The sailor chugs his beer."), 9);
    next;

    select
        lg("Excuse me? Do you know who I am?");

    mes "";
    mesn;
    mesq l("Don't do theee... *hic* with me eh!");
    next;

    menu
        l("Are you ok?"), L_Quit,
        l("Alright... Bye."), -;

    mes "";
    mesn;
    mesq lg("No and *hic*... No, you and you and your... *burp* stup*hic* guild!");
    next;
    mesq l("You tried to get rid of me, eeh? But surprise! I'm still here... *hic* Or there...");
    next;
    mesq l("But you won't *hic* me this time...");
    next;

    menu
        l("What are you talking about? What guild?"), L_Quit,
        l("You are full of wine, my friend..."), -,
        l("You should go and get some sleep."), L_Quit;

    mes "";
    mesn;
    mesq l("If I saw *hic* who you were... *hic* Would not have helped you! ");
    next;

    menu
        l("But who am I?"), -,
        l("What am I supposed to say?"), L_Quit,
        l("You should go and get some sleep."), L_Secret;

    mes "";
    mesn;
    mesq l("The giant boogeyman!");

    close;

L_Secret:
    mes "";
    mesn;
    mesq l("Hear me *hic* well, what ever, whatididever you will *hic* said ab... uhm... out what?! You saw there, the Legion of Aemil won't let it get public.");
    next;
    mesq l("Nobody will know about the existence of the Mercurians.");
    next;
    mesq l("Nobody! *burp*");
    next;

    close;

L_Hic:
    npctalk3 l("*Hic*");

    close;

L_Quit:
    mes "";
    mesn;
    mesq l("Yeah you're all like *hic* that in Esperia, but you won't get me! *burp*");
    next;
    mesq l("I'm not that numb eeh *hic* what did ever yous disco... ...vered there, the Legion of Aemil won't get me!");
    mesq l("*burp*");
    next;

    mesn "Narrator";
    mesc(l("The sailor turns his back to you."), 9);

    close;

OnInit:
    .bodytype = BODYTYPE_3;
    .distance = 3;
    end;
}
