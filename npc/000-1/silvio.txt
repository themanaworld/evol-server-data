// Evol scripts.
// Authors:
//    Qwerty Dragon
//    Reid
// Description:
//    Perverted and arrogant sailor.

000-1,86,99,0	script	Silvio	NPC_SILVIO,{
    mesn;
    mesq l("Hey!");
    next;
    mesq l("How are you doing?");
    next;

    menu
        l("Hello... Do I know you?"), -,
        rif(getq(ShipQuests_Gugli) > 0 && getq(General_Narrator) == 0, l("Fine, could you please tell me where I can find Gugli's sailors?")), L_Sailors,
        l("Uhm, bye."), L_Quit;

    mes "";
    mesn;
    mesq l("Hehe, no. But I'm sure somebody like you would love to meet somebody like me! Women want me, men want to be like me!");
    next;
    mesq l("So, what good wind brought you here? Were you on your raft to meet me or Julia? Or is it because you've heard of my feats in Artis?");
    next;

    menu
        l("You're right, it's about you."), L_Julia,
        l("What is Artis?"), L_Artis,
        l("None of them?"), -;

    mes "";
    mesn;
   mesq l("You know that I was not always a sailor, right? I was once a very important Person!");
    next;
    mesq l("Before...");
    next;

    setcamnpc "Gugli";
    mes "";
    mesn "Gugli";
    mesq l("Hey, don't worry about him, it's one of the first days in a long time that we can just chill out.");
    next;
    mesq l("Maybe he over did it with the bottle today to celebrate that!");
    next;
    restorecam;

    menu
        l("He's funny, it's not a problem."), -,
        l("Ok, I will leave him alone."), L_ForeverAlone;

    setcamnpc "Gugli";
    mes "";
    mesn "Gugli";
    mesq l("Oh alright, nevermind then.");
    next;
    restorecam;

    mesn;
    mesq l("Ah... Gugli... He's too young to understand our conversation.");
    next;
    mesq l("So, what was I saying?");
    next;

    menu
        l("You told me that you 'were' important."), L_VeryImportantNoob,
        l("You mentioned the quality of your wine."), -;

    mes "";
    mesn;
    mesq l("Oh really? How could I forget something as important as that?!");
    next;
    mesq l("I think that my wine is of great quality! I'm on my second bottle and I already... What were we talking about again?");

    close;

L_VeryImportantNoob:
    mes "";
    mesn;
    mesq l("It's true!");
    next;
    mesq l("But I can't tell you anything about it, I'm sorry.") ;
    next;

    setcamnpc "Gugli";
    mes "";
    mesn "Gugli";
    mesq l("Hey Silvio, it's your turn to carry the package, go!");
    next;
    restorecam;

    mesn;
    mesq l("Duty calls me, *hic*, see you later.");

    close;

L_Julia:
    mes "";
    mesn;
    mesq l("Not to kill your dreams, but i fear me and Julia are romantically entangled and there is no room for a third person ... on the other hand *smirks frivolous*");
    next;
    mesq l("She said she will accept me when pious will have teeth. It's just a matter of time, you see?");
    next;

    menu
        l("I see. Bye!"), L_Quit,
        l("Oh look, there's a piou behind you!"), L_Piou;

L_Piou:
    mes "";
    mesn;
    mesq l("Seriously?!");
    next;
    mesq l("Hurry, hurry! We need to check its teeth!");

    close;

L_Artis:
    mes "";
    mesn;
    mesq l("Artis is a very nice place. There are some hot chicks...");
    next;
    mesq l("The Raijin from the library flirted with me a while ago, so has Enora from the Legion.");
    next;
    mesq l("Oh, and Olga from the marketplace as well!");
    next;
    mesq l("You should look for them when we arrive at the port. You won't regret it! Ehehe...");

    goodbye;

L_ForeverAlone:
    mes "";
    mesn "Narrator";
    mesc(l("Silvio starts to speak to his bottle, you leave, letting him have a private conversation."), 9);

    close;

L_Sailors:
    mes "";
    mesn;
    mesq l("What? Why? They aren't more attractive than me, why do you want to see them?");
    next;
    mesq l("And what would you give me in exchange for that information?");
    next;

    setcamnpc "Gugli";
    mes "";
    mesn "Gugli";
    mesq l("Stop heckling, Silvio, or I'll tell about how you always cry like a little baby during thunderstorms.");
    next;
    restorecam;

    mesn;
    mesq l("I...");
    next;
    mesq l("How could you... We said we wouldn't talk about that again!");
    next;
    mesq l("Tibbo went to the south-east of the island, others are gone to the north, and I think that Gulukan is not so far from here.");
    next;
    mesq l("But anyway, take the road which goes to the north to find everybody.");
    next;
    mesq l("Now, leave me alone...");
    next;

    menu
        l("See you."), L_Quit,
        l("*Imitate a thunder's sound.*"), -;

    closeclientdialog;

    npctalk3 l("Aaaaaahhhhhh!");

    close;

L_Quit:
    closeclientdialog;

    npctalk3 l("Give me a kiss before you say goodbye!");

    close;

OnInit:
    .distance = 2;
    end;
}
