// Evol scripts.
// Authors:
//    Hal9000
//    Qwerty Dragon
//    Reid
// Description:
//    Sailor who gives some informations to the player.

000-1,79,108,0	script	Lean	NPC_ELVEN_MAN_BLONDE,{
    if (getq(General_Narrator) > 0)
    {
        sailortalk;
    }

    .@gugli = getq(ShipQuests_Gugli);

    mesn;
    mesq l("Hi! I can finally see you under the sunlight!");
    next;
    mesq l("I hope you don't mind that we used your raft to build this ramp.");
    next;
    mesq l("We usually don't stop in such splendid places, but the captain let us stay here while he writes down the location of this new island on his map!");
    next;
    mesq l("Some of the crew are looking for goods we can trade with at our next destination.");
    next;

    menu
        rif(.@gugli == 1, l("Well, I was in fact looking for them. Where are they now?")), L_Sailors,
        l("Wait... Where are we going?"), L_Artis;

L_Artis:
    mes "";
    mesn;
    mesq l("Artis of course!");
    next;
    mesq l("It's where every merchant ship end their road and we won't be an exception.");
    next;
    mesq l("If you're looking for us there, most of us will be at the tavern of the Red Plush, at the bar counter, for Silvio's case...");

    goto L_Close;

L_Sailors:
    mes "";
    mesn;
    mesq l("They are all around the island.");
    next;
    mesq l("I'm sure Tibbo is alone in the south-west corner of the island. He believes it's the best place to collect @@s.", getitemlink(Aquada));
    next;
    mesq l("While Jalad and Ale like to work together, as they usually do on the ship, last time I've seen them, they were near the big lake, north from here.");
    next;
    mesq l("They shouldn't be too far from each other.");
    next;

    menu
        l("Thank you for your help."), L_Close,
        l("Gugli mentioned six sailors. What about the others?"), -;

    mes "";
    mesn;
    mesq l("I'm not sure. They probably left the ship early this morning. I was not awake yet.");
    next;
    mesq l("You should ask Maxe. He's an early riser.");

    close;

L_Close:
    goodbye;
    close;

OnInit:
    .bodytype = BODYTYPE_3;
    .distance = 2;
    end;
}
