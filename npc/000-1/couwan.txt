// Evol scripts.
// Authors:
//    Hal9000
//    Qwerty Dragon
//    Reid
// Description:
//    Couwan is trying to scam the player.
// Variable:
//    x   ShipQuests_Couwan
// Values:
//    0   Never talked with Couwan.
//    1   Spoke, and received the quest scam.
//    2   Done quest scam.

000-1,89,36,0	script	Couwan	NPC_TRITAN_MAN_SAILOR,{
    .@q = getq(ShipQuests_Couwan);
    if (.@q == 1)
    {
        mesn;
        mesq l("What are you looking at?");
        next;
        mesq l("Yeye still have my box? Stare less and work more. Go give it to Gugli!");
        close;
    }
    else if (.@q > 1)
    {
        mesn;
        mesq l("What are you looking at?");
        next;
        mesq l("Yeye gave my box to Gugli? Nice, nice yeye!");
        close;
    }

    mesn;
    mesq l("Hello yeye.");
    next;
    mesq l("Look how splendid this landscape is!");
    next;

    switch(select(
        l("Very nice, indeed!"),
        l("I don't see anything else other than... water?")))
    {
        case 2:
            mes "";
            mesn;
            mesq l("Yeye's brain is probably still full of sea water if yeye can't see the beauty of this place.");
            next;
            mesq l("Stupid yeye...");
            close;
    }

    mes "";
    mesn;
    mesq l("Yeye got good eyes and seem to have fully recovered from your injuries...");
    next;
    mesq l("Hey, could yeye please take my box of fish to Gugli?");
    next;

    switch(select(
        l("Sure, but what will I get in exchange?"),
        l("I'm sorry, I don't have time right now.")))
    {
        case 2:
            mes "";
            mesn;
            mesq l("Stupid yeye...");
            break;

        case 255:
            break;

        default:
            mes "";
            mesn;
            mesq l("Yeye ask too much but do too little. Take this box and stop talking.");
            next;

            inventoryplace FishBox, 1;
            mesn "Narrator";
            mesc(l("Couwan hands you a box full of fish."), 9);
            getitem FishBox, 1;
            next;
            mesc(l("The sailor turns his back to you."), 9);

            setq ShipQuests_Couwan, 1;
            break;
    }
    close;

OnInit:
    .sex = GENDER_HIDDEN;
    .distance = 2;
    end;
}
