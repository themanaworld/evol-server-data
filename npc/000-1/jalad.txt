// Evol scripts.
// Authors:
//    Hal9000
//    Qwerty Dragon
//    Reid
// Description:
//    Sailor part of the Gugli quest.
//    Plushroom box.

000-1,46,35,0	script	Jalad	NPC_ELVEN_MAN_TRADER_SITTING,{
    if (getq(General_Narrator) > 0)
    {
        sailortalk;
    }

    .@g = getq(ShipQuests_Gugli);
    if (.@g == 0) goto L_NoTask;

    .@q = getq(ShipQuests_Jalad);
    if (.@q == 1) goto L_CallFunc;

    mesn;
    mesq l("*Uff*, *Argh*, why don't you give me some help?");
    next;
    mesq l("I'm not suited for this kind of work!");
    next;

    menu
        l("I was going to ask you if you would need any help."), L_Continue,
        l("What exactly is your real job?"), -;

    mes "";
    mesn;
    mesq l("I'm the cook's assistant of the ship. I help Gado, La Johanne's Chef, in his daily work.");
    next;
    mesq l("You should go see him. He is one of the most experienced sailors we have.");
    next;

    select
        l("I will surely visit him. Do you need any help with your boxes?");

L_Continue:
    mes "";
    mesn;
    mesq l("Surely. Take this box full of @@s.", getitemlink(Plushroom));
    next;

    inventoryplace PlushroomBox, 1;

    setq ShipQuests_Jalad, 1;
    getitem PlushroomBox, 1;

    mes "";
    mesn;
    mesq l("They are so tasty when cooked with some @@. Don't drop any of them!", getitemlink(PiouLegs));

    goto L_Close;

L_CallFunc:
    sailorfood;

    menu
        rif(getq(ShipQuests_Gugli) > 1, l("Task is done.")), L_TaskDone,
        rif(getq(ShipQuests_Jalad) == 1 && (getq(ShipQuests_Ale) == 0 || getq(ShipQuests_Astapolos) == 0 || getq(ShipQuests_Gulukan) == 0 || getq(ShipQuests_QMuller) == 0 || getq(ShipQuests_Tibbo) == 0), l("I'm still busy, I need to find the other sailors.")), -,
        l("What's that?"), L_Plushroom;

    mes "";
    mesn;
    mesq l("Who are you looking for?");
    next;

    menu
        rif(getq(ShipQuests_Ale) == 0, l("Ale.")), L_Ale,
        rif(getq(ShipQuests_Astapolos) == 0, l("Astapolos.")), L_Bottom,
        rif(getq(ShipQuests_Gulukan) == 0, l("Gulukan.")), L_Bottom,
        rif(getq(ShipQuests_QMuller) == 0, l("Q'Muller.")), L_Bottom,
        rif(getq(ShipQuests_Tibbo) == 0, l("Tibbo.")), L_Bottom,
        l("Nobody."), -;

    closeclientdialog;
    close;

L_Bottom:
    mes "";
    mesn;
    mesq l("I've seen him at the bottom of the island, have a look around.");

    close;

L_Ale:
    setcamnpc "Ale";

    next;
    mesn;
    mesq l("Look, there he is!");
    next;

    restorecam;
    close;

L_Plushroom:
    mes "";
    mesn;
    mesq l("'That', as you call it, is a @@. There are plenty on this island!", getitemlink(Plushroom));
    next;
    mesq l("It's a kind of mushroom. We call it like that because of it's taste, just like a marshmallow. It also has this name because of it's appearance. It looks like a plush!");
    next;
    mesq l("There are some groups of these @@s all around this island. Just pick some and have a try.", getitemlink(Plushroom));

    close;

L_NoTask:
    mes "";
    mesn;
    mesq l("Sometimes, I wish someone would be sent here to help us.");

    close;

L_TaskDone:
    mes "";
    mesn;
    mesq l("Perfect!");

L_Close:
    goodbye;

OnInit:
    .bodytype = BODYTYPE_3;
    .distance = 2;
    end;
}
