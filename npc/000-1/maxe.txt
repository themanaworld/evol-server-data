// Evol scripts.
// Authors:
//    Hal9000
//    Qwerty Dragon
//    Reid
// Description:
//    Friend of Sapartan, he gives some information on how to find Gulukan, Astapolos and Q-Muller.

000-1,72,110,0	script	Maxe	NPC_DEMON_MAN_SITTING_UNDER_TREE,{
    if (getq(General_Narrator) > 0)
    {
        sailortalk;
    }

    .@gugli = getq(ShipQuests_Gugli);
    .@knife = getq(ShipQuests_Knife);

    mesn;
    mesq lg("We are glad captain Nard has let you join us down here!");
    next;
    mesq l("Living inside a ship is great, but we sometimes need some fresh air.");
    next;
    mesq l("Anyway, can I help you in any way?");
    next;

    menu
        rif(.@gugli == 0, l("Well, is there anything I can do here to help?")), L_Help,
        rif(.@gugli == 1, lg("Gugli asked me to help the sailors to gather food. Do you know where I can find them?")), L_Sailors,
        rif(.@gugli > 1, l("Seems I've helped everyone here. I don't know what to do now!")), L_Annoyed,
        rif(.@knife == 0, l("It looks like you are an expert of the life inside 'La Johanne'. Do you know any hidden secrets nobody knows?")), L_Secrets,
        l("No thanks, not at the moment."), L_Quit;

L_Help:
    mes "";
    mesn;
    mesq l("You could start by talking to Gugli. He always has tasks for people willing to help!");

    close;

L_Sailors:
    mes "";
    mesn;
    mesq l("I remember I saw a few of them leaving the ship early this morning to get a head start on today's work.");
    next;
    mesq l("I recall seeing Gulukan, Astapolos and Q'Muller.");
    next;

    goto L_MenuSailors;

L_MenuSailors:
    menu
        l("Do you know where can I find Gulukan?"), L_Gulukan,
        l("Any clue on where Astapolos may be hidden?"), L_Astapolos,
        l("What about Q'Muller? Where is he?"), L_Qmuller,
        l("Alright. I will go looking for them now."), L_Quit;

L_Gulukan:
    mes "";
    mesn;
    mesq l("Oh, he is not far away from here. Just take the road through the crocojungle north from here.");
    next;
    mesq l("You will find a wooden sign near a crossroad. He is a few steps on the left.");
    next;

    goto L_MenuSailors;

L_Astapolos:
    mes "";
    mesn;
    mesq l("Astapolos... That guy is shy like a piou!");
    next;
    mesq l("He likes to hide near the little lake in the north-west side of the island.");
    next;

    goto L_MenuSailors;

L_Qmuller:
    mes "";
    mesn;
    mesq l("You need to cross the crocojungle heading north.");
    next;
    mesq l("You can spot Q'Muller on a cliff off toward the west, busy collecting @@s.", getitemlink(Plushroom));
    next;

    goto L_MenuSailors;

L_Annoyed:
    mes "";
    mesn;
    mesq l("There is always something you can do here, like killing some tortugas or helping Peter clean the ship from rattos.");

    close;

L_Secrets:
    mes "";
    mesn;
    mesq l("Nope, there are no secrets hidden within the ship's hull. Some sailors swear they have seen stowaways hidden somewhere. The bow or the stern? Just stories sailors love to tell...");
    next;
    mesq l("Oh! And in one of the rooms of the second deck you can find Gado's knives. Our chef keeps there the sharpest ones... Probably not just to cut some carrots...");
    next;
    mesq l("Go and grab one of them. A good knife will help with your hunting the creatures of this island.");

    close;

OnInit:
    .bodytype = BODYTYPE_3;
    .distance = 2;
    end;

L_Quit:
    goodbye;
}
