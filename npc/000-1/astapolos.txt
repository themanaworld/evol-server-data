// Evol scripts.
// Authors:
//    Hal9000
//    Qwerty Dragon
//    Reid
// Description:
//    Sailor part of the Gugli's quest.
//    Croconut box.

000-1,22,50,0	script	Astapolos	NPC_ELVEN_MAN_RED,{
    if (getq(General_Narrator) > 0)
    {
        sailortalk;
    }

    .@g = getq(ShipQuests_Gugli);
    if (.@g == 0) goto L_NoTask;

    .@q = getq(ShipQuests_Astapolos);
    if (.@q == 1) goto L_CallFunc;

    mesn;
    mesq l("So finally someone has came to visit me?");
    next;

    select
        lg("Actually, I came here to work on my task.");

    mes "";
    mesn;
    mesq l("Oh, I see. That's right. Why should someone come here to see me, seriously?");
    next;
    mesq l("Do you think this is fair? They sent me down here alone, far away from everyone.");
    next;

    menu
        lg("I'm sorry for your situation. They should alternate shifts with other sailors from time to time."), -,
        l("Well if you are whining like that all the time, then I think there is a reason behind their choice."), L_Whyning;

    mes "";
    mesn;
    mesq l("Exactly. Even our task is ridiculous. Why should we collect food down here if we have plenty of those tasty and soft rattos inside our ship, waiting just to be hunted and cooked?");
    next;

    menu
        l("You... You eat... rattos? I think you're just a fool."), L_Whyning,
        l("Oh... eating rattos. Sounds... ehm... Delicious, yes."), -;

    mes "";
    mesn;
    mesq l("You like them, don't you? Just speak with Peter then. He is on the second deck of the ship. He will send you downstairs to the hold, where you will find a furry meal!");
    next;
    mesq lg("Oh, but you didn't come here to talk about that, am I right?");
    next;

    select
        lg("Actually, I was here to help you with those boxes. Can I bring one of them back to Gugli?");

    mes "";
    mesn;
    mesq l("Usually I don't allow anyone to touch my boxes but...");
    next;
    mesq lg("You have been so nice to me. So please, take one.");
    next;

    inventoryplace CroconutBox, 1;

    setq ShipQuests_Astapolos, 1;
    getitem CroconutBox, 1;

    mes "";
    mesn;
    mesq l("Good luck! And remember to visit Peter and our little furry friends whenever you return back onboard!");

    close;

L_CallFunc:
    sailorfood;

    menu
        rif(getq(ShipQuests_Gugli) > 1, l("Task is done.")), L_TaskDone,
        rif(getq(ShipQuests_Astapolos) == 1 && (getq(ShipQuests_Ale) == 0 || getq(ShipQuests_Gulukan) == 0 || getq(ShipQuests_Jalad) == 0 || getq(ShipQuests_QMuller) == 0 || getq(ShipQuests_Tibbo) == 0), l("I'm still busy, I need to find the other sailors.")), -,
        l("How are things going?"), L_HowAreYou,
        l("Who are you?"), L_Who;

    mes "";
    mesn;
    mesq l("Who are you looking for?");
    next;

    menu
        rif(getq(ShipQuests_Ale) == 0, l("Ale.")), L_Top,
        rif(getq(ShipQuests_Gulukan) == 0, l("Gulukan.")), L_Bottom,
        rif(getq(ShipQuests_Jalad) == 0, l("Jalad.")), L_Top,
        rif(getq(ShipQuests_QMuller) == 0, l("Q'Muller.")), L_Bottom,
        rif(getq(ShipQuests_Tibbo) == 0, l("Tibbo.")), L_Bottom,
        l("Nobody."), -;

    closeclientdialog;
    close;

L_HowAreYou:

    mes "";
    mesn;
    mesq l("A sunny and hot day,");
    next;
    mesq l("a quiet place,");
    next;
    mesq l("a ground!");
    next;
    mesq l("I hope that answers your question.") + " " + l("Hehe!");

    close;

L_Who:
    mes "";
    mesn;
    mesq l("My name is Astapolos. Q'Muller and I joined Nard's crew a few years ago when it was just a little merchant ship.");
    next;
    mesq l("At that time, we were selling crab food on our old mushroom island.");
    next;
    mesq l("And now I'm a sailor, as you can see!");

    close;

L_Bottom:
    mes "";
    mesn;
    mesq l("I've seen him at the southern part of the island, check around.");

    close;

L_Top:
    mes "";
    mesn;
    mesq l("I've seen him at the northern part of the island.");
    next;
    mesq l("You should walk to the north.");

    close;

L_NoTask:
    npctalk3 l("Just leave me alone.");

    closeclientdialog;
    close;

L_TaskDone:
    mes "";
    mesn;
    mesq l("Perfect!");

    close;

L_Whyning:
    npctalk3 l("Then leave me alone.");

    closeclientdialog;
    close;

OnInit:
    .distance = 2;
    end;
}
