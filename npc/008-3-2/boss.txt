// The Mana World Script
// Author:
//  Jesusalva
// Description:
//  Controls the boss on 008-3-2 and the Master Book Learning
// see npc/items/master_skillbook.txt for explanation about variables

008-3-2,0,0,0	script	#BossCtrl_008-3-2	NPC_HIDDEN,{
    end;

// Test server: 15 minutes only
OnTimer900000:
    if (!debug)
        end;

// Otherwise, respawn every hour
OnTimer3600000:
    stopnpctimer;
OnInit:
    $@MB_00832=0;
    monster "008-3-2", 67, 30, strmobinfo(1, SpiderQueen), SpiderQueen, 1, "#BossCtrl_008-3-2::OnBossDeath";
    end;

OnBossDeath:
    initnpctimer;
    BossSlain(.name$, "$@MB_00832");
    end;

OnBossCheck:
    @mb_BossId=-1;
    // TODO: Check if you really fought or was just lurking
    // Check if party is correct
    if (getcharid(1) != $@MB_00832)
        end;
OnBegin:
    @mb_BossId=SpiderQueen;
    @mb_SkillId=TF_POISON;
    @mb_ItemId=MagicFeather; // Placeholder
    @mb_ItemAm=1;
    addtimer(15000, "#MasterBook::OnUnset");
    end;

}
