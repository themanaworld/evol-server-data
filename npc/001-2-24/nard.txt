// Evol scripts.
// Authors:
//    Qwerty Dragon
//    Reid
// Description:
//    Captain Nard dialogs.
//    Nard does the introduction with a small quest.
//    This quest is meant to teach the player how to use a weapon,and to bring food to the ship.
//    4 bits array:
//    ShipQuests
// Variable:
//    0    ShipQuests_Nard
//    1    ShipQuests_Gugli
//    2    ShipQuests_ChefGado
// Values:
//    00   Introduction of the boxes and Nard. This is displayed when the player never spoke to Nard or his box.
//    01   Nard spoke and gave access to the outdoor of the ship.
//    02   Completed the Gugli quest.
//    03   ChefGado Quest accepted.
//    04   ChefGado Quest completed and "Introduction" chapter finalized.
//    05   Reward taken from the box.
//    10   Never talked with Gugli.
//    11   Gugli gives you the task.
//    12   Gave all of the boxes to Gugli.

001-2-24,25,26,0	script	Nard#Artis	NPC_NARD,{

    checkclientversion;
    mesn;
    mesq g(l("Good day miss."), l("Good day mister."));
    next;
    mesq l("What can I do for you?");
    .@q = getq(ArtisQuests_Enora);

    // Before the player sees Enora, the menu shows L_Lost on top
    // If the player saw Enora, the menu shows L_Lost at the bottom
    // Because he shouldn't be lost after having talked to her, so the priority in menu changes
    menu
        rif(.@q == 0, l("I'm lost, where should I go?")), L_Lost,
        rif(.@q > 0, l("Tell me, where are we right now?")), L_Info,
        l("How long will you stay here?"), -,
        rif(.@q > 0, l("I'm lost, where should I go?")), L_Lost;

    speech S_FIRST_BLANK_LINE | S_LAST_NEXT,
        l("We just returned from a long journey and Artis is a good city to rest and to conclude our exchange."),
        l("I think that we will stay in port for a great time."),
        l("Although, if you want to visit a place nearby, we could take you there."),
        l("Do you want to go somewhere?");

    switch (select(l("Drasil Island."),
                   l("Nowhere, I'm fine here.")))
    {
        case 1:
            speech S_FIRST_BLANK_LINE | S_LAST_NEXT,
                l("Let's set sail then!");

            savepoint "000-2-1", 50, 38;
            if (!getmapxy(.@map$, .@x, .@y, 0))
            {
                warp "000-2-3", .@x, .@y;
            }
            else
            {
                warp "000-2-3", 22, 27;
            }

            closeclientdialog;
            close;
        case 2:
            speech S_FIRST_BLANK_LINE | S_LAST_NEXT,
                l("Ok we stay docked here if you need our help.");

            closeclientdialog;
            close;
    }
L_Info:
    closeclientdialog;
    npctalk3 l("Don't you recognize the great city of Artis?");
    close;

L_Lost:
    mes "";
    mesn;
    mesq l("Julia made an appeal to the Legion of Aemil to get in touch with you, exit the ship and you should see your escort!");
    next;

    closeclientdialog;
    close;

OnInit:
    .distance = 5;
    end;
}
