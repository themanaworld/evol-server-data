// Evol scripts.
// Authors:
//    Ablu
//    Qwerty Dragon
// Description:
//    Introduction scene where two sailors talk about how the player was found on his raft.
//    Elmo will handle dialogs between both NPCs.

000-0-0,26,23,0	script	Elmo#sailors	NPC_ELMO,{

OnTalk:
    deltimer("Elmo#sailors::OnSlow");
    setcamnpc;
    mesn "Elmo";
    mesq lg("This girl is lucky we found her before a shark did. I have no idea where she comes from. By the way, did you see the logo on her raft?", "This guy is lucky we found him before a shark did. I have no idea where he comes from. By the way, did you see the logo on his raft?");
    next;

    mesn "Magic Arpan";
    mesq lg("Yeye, it's the logo of the Legion of Aemil in Esperia! I wonder what this yoiis was doing so far away from the coast.");
    next;

    mesn "Elmo";
    mesq l("I wonder too...");
    next;

    mesn "Magic Arpan";
    mesq lg("Maybe she was one of those who got lost last month. She could be the yoiis from Esperia who got a secret diplomatic mission from the Legion of Aemil!", "Maybe he was one of those who got lost last month? He could be the yoiis from Esperia who got a secret diplomatic mission from the Legion of Aemil!");
    next;

    mesn "Elmo";
    mesq l("Makes sense. Do you think we should inform the capt'n about it?");
    next;

    mesn "Magic Arpan";
    mesq l("Yeye.");
    next;

    mesn "Elmo";
    mesq lg("Alright, I'm going to his room, keep an eye on her, we still don't know if she's friend or foe...", "Alright, I'm going to his room, keep an eye on him, we still don't know if he's friend or foe...");
    next;

    mesn "Magic Arpan";
    mesq l("I will yaying do.");
    next;

    mesn "Elmo";
    mesq lg("Oh, and give her some clothes, the poor girl, the ones she had were in an even worse condition than the ones we have.", "Oh, and give him some clothes, the poor guy, the ones he had were in an even worse condition than the ones we have.");
    next;

    mesn "Magic Arpan";
    mesq l("Yayayaya, it's the first time someone is dressed worse than us!");
    next;

    mesn "Elmo";
    mesq l("Hehe... Ok, I'm going to inform the capt'n up there.");
    next;

    mesn "Magic Arpan";
    mesq lg("I'll give her everything she needs, don't worry.", "I'll give him everything he needs, don't worry.");
    next;

    mesn "Elmo";
    mesq lg("Ok, I think she's waking up, go see her.", "Ok, I think he's waking up, go see him.");
    next;

    restorecam;
    adddefaultskills;
    setq General_Narrator, 0;
    addtimer(45000,"Magic Arpan::OnSlow");
    warp "000-2-1", 50, 38;
    savepoint "000-2-1", 50, 38;
    percentheal 100, 100;
    closeclientdialog;
    close;

OnInit:
    .sex = G_MALE;
    end;

OnSlow:
    npctalk3(l("(Click on us to continue the introduction.)"));
    dispbottom(l("Click on the NPCs (Non-Player Characters) around you to continue the introduction."));
    addtimer(60000,"Elmo#sailors::OnSlow");
    close;
}

000-0-0,25,23,0	script	Magic Arpan#sailors	NPC_MAGIC_ARPAN,{
    doevent "Elmo#sailors::OnTalk";
    close;

OnInit:
    .sex = G_MALE;
    end;
}
