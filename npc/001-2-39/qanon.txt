// Evol scripts.
// Authors:
//    Alige
//    Reid
//    Jesusalva
// Description:
//    Artis's Legion of Aemil leader.
// Variables:
//    Artis_Legion_Progress
// Values:
//    5    Sent by Lozerk.
//    6    Train forever (Cannot resume legion, must see brotherhood first)
//          Technically, Q'Anon wants to see if player will recover his memory.
//          After all, player memories might have something important.
//    7    Arrived at Hurnscald (This means we can resume legion)

001-2-39,34,41,0	script	Q'Anon	NPC_Q_ANON,{
    function legionState;
    function legionState2;
    function notaMember;
    .@q=getq(Artis_Legion_Progress);
    mesn;
    mesq lg("It's been a while, girl!", "It's been a while, boy!");
    next;
    if (.@q < 5)
        notaMember();

    // Faction report
    mesc l("Your current standing with the Legion: %s", faction_standing("LEGION", false));
    if (LEGION_RANK)
        mesc l("You are a Legion's %s", legionrank());

    // Main Menu
    select
        rif(.@q == 5, l("Lozerk told me to talk to you and join the legion.")),
        rif(.@q == 6, l("Have I grown enough reputation to join?")),
        l("Thanks, sir Q'Anon.");
    mes "";
    switch (@menu)
    {
        case 1:
            legionState();
            break;
        case 2:
            legionState2();
            break;
        case 3:
            mesn;
            mesq lg("Make me proud, girl.", "Make me proud, boy.");
            break;
    }
    close;

function notaMember {
    mesn;
    mesq col(l("*ahem*"), 9) + " " + l("Heh, did you saw me at the port? No?");
    next;
    mesn;
    mesq l("This is how good we Legion Members are. I was informed of your arrival way before you left Drasil Island.");
    next;
    mesn;
    mesq l("I usually would not care, but it is not every day a complete stranger arrives at Artis.");
    next;
    mesn;
    mesq l("You're welcome to stay here as long as you wish. Just don't do anything stupid, like a riot, for example.");
    close;
    return;
}

function legionState {
    mesn;
    mesq l("Oh? And do you think you have what it takes to be a proud Legion Member?");
    next;
    mesn;
    mesq lg("Listen, kid. Being a legion member is not a joke.");
    next;
    mesn;
    mesq l("Hmm, why don't you go train a little more? We have several training rooms here.");
    next;
    mesn;
    mesq l("Yes, I am authorizing you to use them as you deem fit, until you get stronger.");
    next;
    mesn;
    mesq l("Also, build a reputation with the Legion. Talk to everyone. Help everyone. Prove me your worth!");
    next;
    mesn;
    mesq l("Only after you prove yourself, you may join our ranks as a proud member! Hahah!");
    setq Artis_Legion_Progress, 6;
    // So, we probably should use Clan System for the major guilds.
    // I guess major guilds are: Legion, Brotherhood
    // minor guilds: Merchants, Thieves and Mages.

    // It would be the commands below:
    // join_clan(FACTION_LEGION);
    // clan_leave();
    return;
}

function legionState2 {
    if (faction_standing("LEGION", true) >= 1) {
        mesn;
        mesq l("Actually. I am thinking in sending you in a... special mission.");
        next;
        mesn;
        mesq l("You probably noticed a frigate docked near La Johanne when you arrived?");
        next;
        mesn;
        mesq l("We've found... the old continent, from where our race came from long ago.");
        next;
        mesn;
        mesq l("It seems to have fallen in barbarian hands since the Fleet of Ancea, and that, is not something we can allow.");
        next;
        mesn;
        mesc l("%s gives you a piece of paper, which you put on a different pocket.");
        mesq lg("Go to the ship, show the captain this letter if asked. After reaching, look for Captain Hal. He'll give you further instructions.");
        next;
        mesn;
        mesq lg("Make me proud, girl.", "Make me proud, boy.");
        setq Artis_Legion_Progress, 7;
    } else {
        mesn;
        mesq l("Hmm... No, not yet. Try helping other legion members around.");
    }
    return;
}

OnInit:
    .sex = GENDER_HIDDEN;
    .distance = 3;
    end;
}

